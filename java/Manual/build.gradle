// **********************************************************************
//
// Copyright (c) 2003-2016 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

['lifecycle', 'printer', 'simple_filesystem'].each {
    // abc_def -> AbcDef
    def name = it.split('_').collect { it.capitalize() }.join()
    project(":demoManual${name}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            compile localDependency("ice")
            if (!useLocalOnly) {
                runtime "org.apache.tools:bzip2:1.0"
            }
        }

        // Don't create the default jar.
        jar.enabled = false

        clean {
            delete fileTree(dir: 'db', excludes:['.gitignore', 'DB_CONFIG'])
        }

        demoJar("client")
        demoJar("server")
    }
}
